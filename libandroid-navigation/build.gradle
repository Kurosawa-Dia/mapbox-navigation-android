apply plugin: 'com.android.library'

android {
  compileSdkVersion androidVersions.compileSdkVersion
  buildToolsVersion androidVersions.buildToolsVersion

  defaultConfig {
    minSdkVersion androidVersions.minSdkVersion
    targetSdkVersion androidVersions.targetSdkVersion
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true

    buildConfigField('String',
        "MAPBOX_NAVIGATION_SDK_IDENTIFIER", String.format("\"%s\"", "mapbox-navigation-android")
    )
    buildConfigField('String',
        "MAPBOX_NAVIGATION_VERSION_NAME", String.format("\"%s\"", project.VERSION_NAME)
    )
    buildConfigField "String",
        "MAPBOX_NAVIGATION_EVENTS_USER_AGENT", String.format("\"mapbox-navigation-android/%s\"",
        project.VERSION_NAME
    )
    consumerProguardFiles 'proguard-consumer.pro'
  }

  configurations {
    javadocDeps
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  testOptions {
    unitTests.returnDefaultValues = true
    unitTests.includeAndroidResources = true
    unitTests.all {
      jacoco {
        includeNoLocationClasses = true
      }
    }
  }
}

dependencies {
  api dependenciesList.mapboxEvents
  api dependenciesList.mapboxSdkServices
  api dependenciesList.mapboxSdkTurf

  // Navigator
  implementation dependenciesList.mapboxNavigator

  // Support
  implementation dependenciesList.supportAppcompatV7

  // Logging
  implementation dependenciesList.timber

  // AutoValues
  annotationProcessor dependenciesList.autoValue
  compileOnly dependenciesList.autoValue

  // EH dependencies
  implementation 'com.google.guava:guava:27.0.1-android'
  implementation group: 'org.apache.httpcomponents', name: 'httpcore-nio', version: '4.4.10'
  implementation group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.4'
  implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
  implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'
  implementation group: 'log4j', name: 'log4j', version: '1.2.17'
  implementation 'com.google.protobuf:protobuf-java:3.6.1'

  // Unit testing
  testImplementation dependenciesList.junit
  testImplementation dependenciesList.mockito
  testImplementation dependenciesList.hamcrest
  testImplementation dependenciesList.commonsIO
  testImplementation dependenciesList.robolectric
}

apply from: 'javadoc.gradle'
apply from: "${rootDir}/gradle/mvn-push-android.gradle"
apply from: "${rootDir}/gradle/checkstyle.gradle"
apply from: "${rootDir}/gradle/jacoco.gradle"
apply from: "${rootDir}/gradle/dependencies-graph.gradle"
apply from: "${rootDir}/gradle/dependency-updates.gradle"